// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:
    // Handle increment
    Inc16(in=regOut, out=incremented);
    
    // Select between current value and incremented value
    Mux16(a=regOut, b=incremented, sel=inc, out=afterInc);
    
    // Select between inc result and input value
    Mux16(a=afterInc, b=in, sel=load, out=afterLoad);
    
    // Select between previous result and reset (0)
    Mux16(a=afterLoad, b[0..15]=false, sel=reset, out=nextVal);
    
    // Store the result in register
    Register(in=nextVal, load=true, out=out, out=regOut);
}
